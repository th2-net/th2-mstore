plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
}

ext {
    sharedDir       = file("${project.rootDir}/shared")
    cradleVersion   = '3.1.2-dev-version-3.1-2664462627-SNAPSHOT'
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11


repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }

    mavenCentral()

    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
            'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
            'Specification-Title':      '',
            'Specification-Vendor':     'Exactpro Systems LLC',
            'Implementation-Title':     project.archivesBaseName,
            'Implementation-Vendor':    'Exactpro Systems LLC',
            'Implementation-Vendor-Id': 'com.exactpro',
            'Implementation-Version':   project.version
        )
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:3.0.0')

    implementation 'com.exactpro.th2:common:3.37.2'

    implementation 'com.google.protobuf:protobuf-java-util'

    implementation "org.jetbrains:annotations"

    implementation "com.exactpro.th2:cradle-core:$cradleVersion"
    implementation "com.exactpro.th2:cradle-cassandra:$cradleVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams = true
    }
}

application {
    mainClassName = 'com.exactpro.th2.mstore.MessageStore'
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
