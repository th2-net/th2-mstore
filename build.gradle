plugins {
    id "application"
    id "maven-publish"
    alias(libs.plugins.th2.publish)
    alias(libs.plugins.th2.component)
}

group = 'com.exactpro.th2'
version = release_version

repositories {
    mavenCentral()

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    implementation(libs.th2.common)
    implementation(libs.th2.common.utils) {
        because("executor service utils is used")
    }
    implementation(libs.th2.task.utils)

    implementation 'com.google.protobuf:protobuf-java-util'

    implementation 'com.fasterxml.jackson.core:jackson-databind'

    implementation 'io.prometheus:simpleclient'
    implementation "org.jetbrains:annotations"

    implementation 'io.netty:netty-buffer'

    implementation(libs.cradle.core)
    implementation(libs.cradle.cassandra)

    implementation 'org.apache.commons:commons-lang3'

    implementation "org.slf4j:slf4j-api"
    testImplementation(libs.junit.jupiter)
    testImplementation(libs.mockito.junit.jupiter)
    testRuntimeOnly('org.junit.platform:junit-platform-launcher') {
        because 'This artifact will not be added automatically since gradle 9.0. You can read more information here:' +
                'https://docs.gradle.org/8.3/userguide/upgrading_version_8.html#test_framework_implementation_dependencies'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

application {
    mainClassName = 'com.exactpro.th2.mstore.MessageStore'
}

dependencyCheck {
    suppressionFile = file('suppressions.xml')
}